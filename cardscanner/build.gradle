plugins {
    id 'com.android.library'
    id 'maven-publish'
}
def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--always'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return "1.0";
    }
}
android {
    namespace 'company.tap.cardscanner'
    compileSdkVersion 35


    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 1
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        buildConfig = true
    }

}



dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api 'androidx.appcompat:appcompat:1.6.0'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'


    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.google.android.gms:play-services-base:17.2.1'
    api platform("org.jetbrains.kotlin:kotlin-bom:1.8.22")
    // ML Kit
    api 'com.google.mlkit:barcode-scanning:17.3.0'
    api 'com.google.mlkit:text-recognition:16.0.1'
    api 'com.google.mlkit:vision-common:17.3.0'   // ðŸ‘ˆ add this


    api "androidx.camera:camera-core:1.5.0"
    api "androidx.camera:camera-camera2:1.5.0"
    api "androidx.camera:camera-lifecycle:1.5.0"
    api "androidx.camera:camera-view:1.5.0"

    api 'jp.wasabeef:blurry:4.0.1'

}


// Create sources and Javadoc JARs
task releaseSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

task releaseJavadoc(type: Jar) {
    archiveClassifier.set('javadoc')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            create("release", MavenPublication) {
                // Publish AAR
                artifact("$buildDir/outputs/aar/${project.name}-release.aar") {
                    builtBy tasks.named("assembleRelease")
                }

                // Attach sources and javadoc
                artifact(tasks.named("releaseSourcesJar"))
                artifact(tasks.named("releaseJavadoc"))

                groupId = "com.github.Tap-Payments"
                artifactId = "TapCardScannerKit-Android"
                version = "0.0.12"

                // Variant-aware: ensure dependencies are included in POM
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.api.allDependencies.each { dep ->
                        def depNode = dependenciesNode.appendNode('dependency')
                        depNode.appendNode('groupId', dep.group)
                        depNode.appendNode('artifactId', dep.name)
                        depNode.appendNode('version', dep.version)
                        depNode.appendNode('scope', 'compile')
                    }
                }
            }
        }
    }

    // Ensure metadata waits for sources/javadoc
    tasks.named("generateMetadataFileForReleasePublication") {
        dependsOn(tasks.named("releaseSourcesJar"))
        dependsOn(tasks.named("releaseJavadoc"))
    }
}
